finish
/clear

D=30e-3
rho=7800
H=1
E=2e11
mu=0.3
v0=0
deltaH=1e-3
V=sqrt(2*9.8*H)		!Скорость в момент контакта


/prep7
	et,1,plane182 ! 4-х узловой элемент
	keyopt,1,3,1  ! осесиметричная задача

	mp,EX,1,E
	mp,prxy,1,mu
	mp,dens,1,rho

! построение "снизу вверх"	
!	k,1,0,H+D/2
!	k,2,D/2,H
!	k,3,0,H-D/2
!	larc,1,3,2,D/2
!	l,1,3
!	al,all

! построение "сверху вниз" (строим полную окружность, вычитаем квадрат)
	cyl4,0,D/2+deltaH,D/2 ! построили круг
	rectng,0,D,deltaH,deltaH+D ! построили прямоугольник
	aina,all ! пересекаем все


	esize,D/20
	amesh,all

	lsel,s,loc,x,0
	dl,all,,ux
	allsel
	
	acel,0,9.8,0

! моделирование основания
	k,100,0,0
	k,101,2*D,0
	l,100,101
	MP,MU,1,0.0 ! коэффициент трения
	R,3 
	REAL,3  
	ET,2,TARGE169 ! элементы цели
	ET,3,CONTA172 ! элементы контакта
	KEYOPT,3,9,0 ! параметры контактной задачи
	KEYOPT,3,10,2 
! Generate the target surface   
	LSEL,S,loc,y,0
	TYPE,2 ! выбираем элементы цели
	LMESH,all ! разбиваем линию

	TSHAP,PILO
	E, NODE(0,0,0) ! создается пилотный узел
 
	LSEL,S,,,4  ! выбираем линию контакта
	TYPE,3   ! тип элемента - контакт
	NSLL,S,1 ! выбираем все узлы
	ESLN,S,0 ! выбираем все элементы этих узлов
	ESURF    ! строим элементы поверх сетки
	
	esel,s,type,,2 ! выбираем элементы основания
	ESURF,,REVERSE ! переворачиваем нормали
	allsel ! выбираем все
	
	d,node(0,0,0), all ! закрепляем основание
 
finish

/solution
	antype,trans ! анализ переходных процессов
    nlgeom,on ! большие перемещения
	outres,all,all ! выдавать результаты на каждом шаге
	timint,off
	da,all,uy,-deltaH
	time,deltaH/V
	nsubst,1	
	solve
    
	timint,on	! включаем интергирование
	dadele,all,uy	!удаляем жесткое смещение
	
	!contact
	time,0.0004
	nsubst,1000
	solve
	
	time,sqrt(2*H/9.8)*2 ! конечное время умнож на полупериод
	nsubst,1000
	solve
	
	!nsubst,10000,10000,1000 ! начальное количество разбиений


	!solve
finish