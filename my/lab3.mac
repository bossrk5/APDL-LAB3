!плоская задача
FINISH
/CLEAR


D=30e-3
rho=7800
H=1
E=2e11
mu=0.3
V0=0

/PREP7
! симметричная постановка
		ET,1,plane182	! 4-х узловой элемент
		! для этого элемента надо указать, что он в осесимметричной постановке рассматривается
		KEYOPT,1,3,1	! осесиметричная задача
		
		!Материал
		MP,EX,1,E
		MP,PRXY,1,mu
		MP,DENS,1,rho
				
! рисуем половину окружности булевой операцией вычитаем из окружности квадрат
! или по 3ем точкам дугу с линией

! Вариант 1: по 3ем точкам "снизу вверх":
! Строим ключевые точки
		! K,1,0,H+D/2
		! K,2,D/2,H
		! K,3,0,H-D/2
		
		! !строим дугу
		! LARC,1,3,2,D/2
		! !линия
		! L,1,3
		! AL,all
		
! Вариант 2: строим полную окружность и из нее вычитаем квадрат или пересечь квадрат справа "сверху вниз"
		CYL4,0,H,D/2			! построили круг
		RECTNG,0,D,H-D,H+D		! построили прямоугольник
		
		! пересекаем и остается половинка круга
		AINA,all				! пересекаем все
		
		ESIZE,D/20				! размер К.Э
		AMESH,all
		
		LSEL,s,loc,x,0			! выбираем линию с коорд Х=0 для симметрии
		DL,all,,ux			 	! запрещ перемещения по Х на оси симметрии
		ALLSEL					! Снятие выделения
		
		! действует ускорение свободного падения (по оси У), противополож силе тяжести
		ACEL,0,9.8,0			! ускорение
		
		! моделируем жесткое основание, проведем линию из 0 в +
! моделирование основания:
		
		! ключевые точки (номера задали больше заведомо)
		K,100,0,0
		K,101,2*D,0
		L,100,101
		! Pair Based Contact Manager
		
		MP,MU,1,0.0				! коэфф трения
		
		R,3 
		REAL,3  
		ET,2,169			! элементы цели абс жесткие
		ET,3,172			! Элементы контакта
		
		KEYOPT,3,9,0
		KEYOPT,3,10,2   
		
		! Generate the target surface   
		LSEL,S,loc,y,0			! выбираем линию основания 
		TYPE,2 					! выбираем элементы цели
		LMESH,ALL  				! разбиваем линию
		
		! Create a pilot node   
		TSHAP,PILO  
		E,NODE(0,0,0)  			! пилотный узел
		
		! Generate the contact surface  
		LSEL,S,,,4  			! линия контакта
		TYPE,3  				! тип элемента - контакт	
		NSLL,S,1				! выбираем все узлы на линии
		ESLN,S,0				! выбираем все элементы этих узлов
		ESURF   				! поверх существующей сетки накладываем новые элементы
		
		ESEL,s,type,,2			! выбираем элементы основания
		ESURF,,REVERSE			! переворачиваем нормали
		ALLSEL					! выбираем все

		D,node(0,0,0),all		! закрепляем основание
		
FINISH

/solution
		antype,trans			! анализ переходных процессов (транзиентный)
		TIME,sqrt(2*H/9.8)*3	! конечное время
		NSUBST,100000				! начальное число разбиений шагов  (10 - не решится)
		NLGEOM,ON				! большие перемещения
		OUTRES,ALL,ALL			! выдавать промежуточные результаты
		SOLVE
FINISH
